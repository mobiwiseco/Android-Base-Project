apply plugin: 'com.android.application'

def BOOLEAN = 'boolean'
def STRING = 'String'
def INT = 'int'
def TRUE = 'true'
def FALSE = 'false'

def GIT_SHA = 'GIT_SHA'
def BUILD_TIME = 'BUILD_TIME'

def CONNECTION_TIMEOUT = 'CONNECTION_TIMEOUT'
def REST_ENDPOINT = 'REST_ENDPOINT'

android {

  publishNonDefault true
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion "${rootProject.ext.buildToolsVersion}"

  defaultConfig {

    applicationId "co.mobiwise.android.baseproject"

    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion

    versionCode rootProject.ext.versionCode
    versionName rootProject.ext.versionName

    buildConfigField STRING, GIT_SHA, "\"${rootProject.ext.gitSha}\""
    buildConfigField STRING, BUILD_TIME, "\"${rootProject.ext.buildTime}\""
    buildConfigField INT, CONNECTION_TIMEOUT, '2000'
  }

  signingConfigs {
    debug {
      storeFile file('../distribution/debug.keystore')
    }

    release {
      keyAlias ''
      keyPassword ''
      storeFile file('../distribution/release.jks')
      storePassword ''
    }
  }

  productFlavors {
    dev {
      applicationId "${project.android.defaultConfig.applicationId}.dev"
      versionName "${project.android.defaultConfig.versionName}-beta-${rootProject.ext.gitSha}"
      buildConfigField STRING, REST_ENDPOINT, '""'
    }
    prod {
      applicationId project.android.defaultConfig.applicationId
      buildConfigField STRING, REST_ENDPOINT, '""'
    }
  }

  buildTypes {

    debug {
      applicationIdSuffix ".debug"
      signingConfig signingConfigs.debug
      versionNameSuffix "-debug"
    }
    release {
      minifyEnabled true
      zipAlignEnabled true
      shrinkResources true
      debuggable false
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  //google
  compile 'com.google.code.gson:gson:2.3.1'

  //supportLib
  compile rootProject.ext.supportLib
  compile rootProject.ext.supportLibAppCompat
  compile rootProject.ext.supportLibDesign
  compile rootProject.ext.supportLibRecyclerview
  compile rootProject.ext.supportLibCardview

  //play-services
  compile rootProject.ext.playServicesAnalytics

  //square
  compile 'com.squareup.retrofit:retrofit:1.9.0'
  compile "com.squareup.okhttp:okhttp-urlconnection:${rootProject.ext.okhttpLibVersion}"
  compile "com.squareup.okhttp:okhttp:${rootProject.ext.okhttpLibVersion}"
  compile 'com.squareup:otto:1.3.8'
  compile 'com.squareup.picasso:picasso:2.5.2'

  //jakewharton
  compile 'com.jakewharton.timber:timber:3.1.0'
  compile rootProject.ext.butterKnife

  //facebook
  compile "com.facebook.stetho:stetho:${rootProject.ext.stethoVersion}"
  compile "com.facebook.stetho:stetho-okhttp:${rootProject.ext.stethoVersion}"

  compile 'com.afollestad:material-dialogs:0.7.7.0'
}